# Maintainer: Daniel Isenmann <daniel@archlinux.org>

pkgname=gegl
pkgver=0.4.28+1+g663009f56
pkgrel=1
pkgdesc="Graph based image processing framework"
arch=('x86_64')
url="https://www.gegl.org/"
license=('GPL3' 'LGPL3')
groups=('modified')
depends=('babl' 'libspiro' 'json-glib' 'libgexiv2' 'libraw' 'openexr' 'librsvg' 'libtiff' 'luajit' 'jasper' 'lensfun' 'suitesparse' 'poppler-glib' 'libnsgif' 'python-numpy')
makedepends=('intltool' 'ruby' 'mesa' 'glu' 'ffmpeg' 'exiv2' 'vala' 'gobject-introspection' 'git' 'meson' 'python-gobject')
optdepends=('ffmpeg: ffmpeg plugin')
_commit='663009f567eeae5ee19eb70d49e1d2c7816317fc'  # tags/GEGL_0_4_28^0
source=("git+https://gitlab.gnome.org/GNOME/gegl.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^GEGL_//;s/_/./g;s/-/+/g'
}

#prepare() {
#  cd ${pkgname}
#  git cherry-pick -n c1c3541caab485384087bbbcf1c83ef2d5bd4d43
#}

build() {
  mkdir -p build
  cd build
  arch-meson ../$pkgname \
    -Dworkshop=true \
    -Dmrg=disabled
  ninja
}

check() {
  cd build
  ninja test || :
}

package() {
  cd build
  DESTDIR="${pkgdir}" ninja install
}
